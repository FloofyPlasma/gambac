plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'babric-loom-extension' version '1.6-SNAPSHOT'
}

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

base.archivesName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	clientOnlyMinecraftJar()
	accessWidenerPath = file("src/main/resources/gambac.accessWidener")
	customMinecraftManifest.set("https://floofyplasma.github.io/aabric-libraries/manifests/${minecraft_version}.json")
	intermediaryUrl.set("https://floofyplasma.github.io/aabric-libraries/aabric/intermediary/%1\$s/intermediary-%1\$s-v2.jar")
}

babric {
	disableM1Fixes.set(true) // Disable Gambáč since thats literraly this project :)
}

repositories {
	// Used for Babric Loom Extensions
	maven {
		name = 'Babric'
		url = 'https://maven.glass-launcher.net/babric'
	}

	// Used for many dependencies
	maven {
		name = 'Glass Releases'
		url = 'https://maven.glass-launcher.net/releases'
	}

	// Used for Snapshots
	maven {
		name = 'Glass Snapshots'
		url = 'https://maven.glass-launcher.net/snapshots'
	}

	// NyaRepo
	maven {
		name = "nyarepo"
		url = "https://maven.fildand.cz/releases"
	}

	// Used for a StationAPI dependency.
	maven {
		name = 'Froge'
		url 'https://maven.minecraftforge.net/'
	}

	// Used for projects that do not have a maven repository, but do have a GitHub repository with working build scripts.
	maven {
		name = 'Jitpack'
		url = 'https://jitpack.io'
	}

	// Aabric :3
	maven {
		name = 'Aabric'
		url = 'https://floofyplasma.github.io/aabric-libraries'
	}

	// Modrinth Maven
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}

	mavenCentral()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "aabric:aarn:${project.yarn_mappings}:v2"
	modImplementation "babric:fabric-loader:${project.loader_version}"

	implementation "org.slf4j:slf4j-api:1.8.0-beta4"
	implementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2'

	/* TODO: ...
	modImplementation ("net.modificationstation:StationAPI:${project.stapi_version}")

	// Optional, but convenient mods for mod creators and users alike.
	modImplementation("net.glasslauncher.mods:ModMenu:${project.modmenu_version}") {
		transitive false
	}
	*/
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

java {
	withSourcesJar()
}

tasks.withType(Jar).configureEach {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	repositories {
		mavenLocal()
		if(project.hasProperty("calmilamsy_secret_publishing_cave_username")){
			maven {
				url = "https://maven.glass-launcher.net/releases"
				credentials {
					username "${project.calmilamsy_secret_publishing_cave_username}"
					password "${project.calmilamsy_secret_publishing_cave_password}"
				}
			}
		}

		if(project.hasProperty("nyarepo_publishing_cave_username")){
			maven {
				url = "https://maven.fildand.cz/releases"
				credentials {
					username "${project.nyarepo_publishing_cave_username}"
					password "${project.nyarepo_publishing_cave_password}"
				}
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}
